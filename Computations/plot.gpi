#!/usr/bin/gnuplot

set terminal pngcairo size 1200,800
set output 'graph.png'

set title 'Proportion of Pairs in Balls in SL(2,Z) that Generate Free Subgroups'

set style line 1 lt 1 lw 2 pt 7 ps 1

set xrange [0:20]
set xtics 0, 5, 20
set mxtics 5

set yrange [0:1]
set ytics 0, 0.25, 1
set mytics 5

set grid lt 1 lw 1.25 dt 3

set key at graph 0.95, 0.125 title 'Generating Sets' box
set key outside

set xlabel 'Ball Radius'
set ylabel 'Proportion of Pairs that Generate Free Subgroups'

plot 'data.txt' index 0 title '(0,-1,1,0), (1,1,0,1)' with lp ls 1 lc rgb '#ff0000', \
     ''         index 1 title '(1,0,1,1), (1,1,0,1)' with lp ls 1 lc rgb '#ff6600', \
     ''         index 2 title '(0,-1,1,1), (1,1,0,1)' with lp ls 1 lc rgb '#ffcc00', \
     ''         index 3 title '(0,-1,1,0), (1,-1,1,0)' with lp ls 1 lc rgb '#33cc33', \
     ''         index 4 title '(1,100,1,101), (1,1,0,1)' with lp ls 1 lc rgb '#0000ff', \
     ''         index 5 title '(1,100,1,101), (1,99,1,100)' with lp ls 1 lc rgb '#9900cc', \
     ''         index 6 title '(2,1,3,2), (1,0,1,1)' with lp ls 1 lc rgb '#660066'
